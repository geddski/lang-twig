// Test grammar for nested mixed languages

@top Template { (directive | Text)* }

directive {
  Insert |
  Conditional { ConditionalOpen (directive | Text)* ConditionalClose }
}

@skip {space} {
  Insert { "{{" DirectiveContent "}}" }
  ConditionalOpen { "{%" kw<"if"> DirectiveContent "%}" }
  ConditionalClose { "{%" kw<"endif"> "%}" }
}

kw<word> { @specialize[@name={word}]<Identifier, word> }

@tokens {
  Identifier { @asciiLetter+ }
  Text { ![{] Text? | "{" (@eof | ![%{] Text?) }
  space { @whitespace+ }
  DirectiveContent { ![%}] DirectiveContent? | $[%}] (@eof | ![}] DirectiveContent?) }
  @precedence { space DirectiveContent }
  "{{" "}}" "{%" "%}"
}

@external propSource twigHighlight from "./highlight.js"
